plugins {
    id "cpp"
    id "google-test-test-suite"
    id "edu.wpi.first.GradleRIO" version "2022.4.1"
}

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project DeployUtils.
deploy {
    targets {
        roborio(getTargetTypeClass('RoboRIO')) {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = project.frc.getTeamNumber()
            debug = project.frc.getDebugOrDefault(false)

            artifacts {
                // First part is artifact name, 2nd is artifact type
                // getTargetTypeClass is a shortcut to get the class type using a string

                frcCpp(getArtifactTypeClass('FRCNativeArtifact')) {
                }

                // Static files artifact
                frcStaticFileDeploy(getArtifactTypeClass('FileTreeArtifact')) {
                    files = project.fileTree('src/main/deploy')
                    directory = '/home/lvuser/deploy'
                }
            }
        }
    }
}

class FileDependencySet implements NativeDependencySet {
    private FileCollection headerFiles;
    private FileCollection emptySet

    FileDependencySet(FileCollection headerFiles, FileCollection emptySet) {
        this.headerFiles = headerFiles
        this.emptySet = emptySet
    }

    @Override
    FileCollection getIncludeRoots() {
        return headerFiles
    }

    /* We'll manually link our files, thank you very much. */
    @Override
    FileCollection getLinkFiles() {
        return null;
    }

    @Override
    FileCollection getRuntimeFiles() {
        return linkFiles
    }
}

def libRCLCPP = new FileDependencySet(
    //project.files('install/include/'),
    project.files('/opt/ros2/galactic/include/'),
    project.files()
)


/* Manually link against libraries here, since we need to link against different libraries depending on architecture */
nativeUtils.platformConfigs.named("linuxx86-64").configure {
    it.cppCompiler.args.add('-L/opt/ros2/galactic/lib')
    it.cppCompiler.args.add('-lrclcpp')

    it.linker.args.add('-L/opt/ros2/galactic/lib')
    it.linker.args.add('-lrclcpp')
    it.linker.args.add('-lrcl')
    it.linker.args.add('-lrcpputils')
    it.linker.args.add('-lrcutils') 
    it.linker.args.add('-llibstatistics_collector')
    it.linker.args.add('-lstatistics_msgs__rosidl_typesupport_cpp')
    it.linker.args.add('-lament_index_cpp')
    it.linker.args.add('-lrcl_yaml_param_parser')
    it.linker.args.add('-lrosgraph_msgs__rosidl_typesupport_cpp')
    it.linker.args.add('-ltracetools')
    it.linker.args.add('-lrcl_interfaces__rosidl_typesupport_cpp')
    it.linker.args.add('-lrmw_implementation')
    it.linker.args.add('-lrcl_logging_interface')
    it.linker.args.add('-lrcl_interfaces__rosidl_typesupport_c')
    it.linker.args.add('-lrcl_logging_spdlog')
    it.linker.args.add('-lrcl_interfaces__rosidl_generator_c')
    it.linker.args.add('-lrosidl_runtime_c')
    it.linker.args.add('-lrosidl_typesupport_cpp')
    it.linker.args.add('-lrosidl_typesupport_c')
    it.linker.args.add('-lspdlog')
    it.linker.args.add('-lbuiltin_interfaces__rosidl_generator_c')
    it.linker.args.add('-lrmw')
    it.linker.args.add('-Wl,-rpath,/opt/ros2/galactic/lib')
}

nativeUtils.platformConfigs.named("linuxathena").configure {
    it.cppCompiler.args.add('-Linstall/lib') 
    it.cppCompiler.args.add('-lrclcpp')
    it.cppCompiler.args.add('-lrcl')
    it.cppCompiler.args.add('-lrcpputils')
    it.cppCompiler.args.add('-lrcutils') 

    it.linker.args.add('-L install/lib') 
    it.linker.args.add('-lrclcpp')
    it.linker.args.add('-lrcl')
    it.linker.args.add('-lrcpputils')
    it.linker.args.add('-lrcutils') 
}

def deployArtifact = deploy.targets.roborio.artifacts.frcCpp

// Set this to true to enable desktop support.
def includeDesktopSupport = true

// Set to true to run simulation in debug mode
wpi.cpp.debugSimulation = false

// Default enable simgui
wpi.sim.addGui().defaultEnabled = true
// Enable DS but not by default
wpi.sim.addDriverstation()

model {
    components {
        frcUserProgram(NativeExecutableSpec) {
            targetPlatform wpi.platforms.roborio
            if (includeDesktopSupport) {
                targetPlatform wpi.platforms.desktop
            }

            sources.cpp {
                source {
                    srcDir 'src/main/cpp'
                    include '**/*.cpp', '**/*.cc'
                }
                exportedHeaders {
                    srcDir 'src/main/include'
                }
            }
            
            binaries.all {
                lib libRCLCPP
            }
            


            // Set deploy task to deploy this component
            deployArtifact.component = it

            // Enable run tasks for this component
            wpi.cpp.enableExternalTasks(it)

            // Enable simulation for this component
            wpi.sim.enable(it)
            // Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
            wpi.cpp.vendor.cpp(it)
            wpi.cpp.deps.wpilib(it)

        }
    }
    testSuites {
        frcUserProgramTest(GoogleTestTestSuiteSpec) {
            testing $.components.frcUserProgram

            sources.cpp {
                source {
                    srcDir 'src/test/cpp'
                    include '**/*.cpp'
                }
            }

            // Enable run tasks for this component
            wpi.cpp.enableExternalTasks(it)

            wpi.cpp.vendor.cpp(it)
            wpi.cpp.deps.wpilib(it)
            wpi.cpp.deps.googleTest(it)
        }
    }
}
